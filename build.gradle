
plugins {
    id 'java'
    id 'application'
    id 'edu.sc.seis.launch4j' version '2.5.3'
}

applicationName = 'TracInstant'
mainClassName = "com.github.tracinstant.TracInstantApp"
version = '0.2.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// For 'application' plugin:
applicationDefaultJvmArgs = [
    "-Xms8M",
    "-Xmx300M",
    "-Dsun.java2d.d3d=false",
    "-Xverify:none",
    "-XX:StringTableSize=60013"
]

repositories {
    mavenCentral()
}

// For runnable .jar:
task fatJar(type: Jar) {

    dependsOn test

    // Simple "fatJar" configuration...
    archiveBaseName = project.name + '-all'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    manifest {
        attributes 'Main-Class': mainClassName

// Alternatively: configure class-path if not building a "fatJar".
//    attributes 'Class-Path': configurations.compile.collect {
//     'libs/' + it.getName()
//    }.join(' ')

    }
}

createExe.dependsOn fatJar

launch4j {
    mainClassName = mainClassName
    jar = '../libs/' + project.tasks['fatJar'].archiveName

    outfile = project.name + "-" + project.version + ".exe"

    icon = '../../icons/TracSlurp.ico'
    jreRuntimeBits = "64/32"
    jvmOptions = applicationDefaultJvmArgs
    jdkPreference = "preferJre"
}

// HACK: until I solve screwy fonts on Java >= 9, also create an alternate exe
task createJava8Exe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask)  {
    dependsOn fatJar

    group = "launch4j"
    description = "Creates an additional EXE that only runs on JRE/JDK 8 (to avoid font issues)"

    jreMaxVersion = "1.8.0_999"
    outfile = project.name + "-" + project.version + "-Java8only.exe"

    icon = '../../icons/TracSlurp.ico'
    jreRuntimeBits = "32/64"
    jvmOptions = applicationDefaultJvmArgs
    jdkPreference = "preferJre"
}

dependencies {

    implementation 'au.com.bytecode:opencsv:2.4'

    // TODO: Would surely help code quality. Find out how to compact with proguard or something.
    // compile 'com.google.guava', version: '21.0'

    testImplementation 'junit:junit:4.12+'
}

