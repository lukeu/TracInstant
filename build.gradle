
plugins {
    id 'java'
    id 'application'
    id 'edu.sc.seis.launch4j' version '2.5.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// For 'application' plugin:
application {
    applicationName = 'TracInstant'
    mainClass = "com.github.tracinstant.TracInstantApp"
    version = '0.2.1'

    applicationDefaultJvmArgs = [
        "-Xms8M",
        "-Xmx400M",
        "-Dsun.java2d.d3d=false",
        "-XX:StringTableSize=240007"
    ]
}

repositories {
    mavenLocal()
    mavenCentral()
}

// For runnable .jar:
task fatJar(type: Jar) {

    dependsOn test

    // Simple "fatJar" configuration...
    archiveBaseName = project.name + '-all'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    manifest {
        attributes 'Main-Class': application.mainClass

// Alternatively: configure class-path if not building a "fatJar".
//    attributes 'Class-Path': configurations.compile.collect {
//     'libs/' + it.getName()
//    }.join(' ')

    }
}

createExe.dependsOn fatJar

launch4j {
    mainClassName = mainClassName
    jar = '../libs/' + file(fatJar.archiveFileName).name

    outfile = project.name + "-" + project.version + ".exe"

    icon = '../../icons/TracSlurp.ico'
    jreRuntimeBits = "64/32"
    jvmOptions = application.applicationDefaultJvmArgs
    jdkPreference = "preferJre"
}

// HACK: until I solve screwy fonts on Java >= 9, also create an alternate exe
task createJava8Exe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask)  {
    dependsOn fatJar

    group = "launch4j"
    description = "Creates an additional EXE that only runs on JRE/JDK 8 (to avoid font issues)"

    jreMaxVersion = "1.8.0_999"
    outfile = project.name + "-" + project.version + "-Java8only.exe"

    icon = '../../icons/TracSlurp.ico'
    jreRuntimeBits = "32/64"
    jvmOptions = application.applicationDefaultJvmArgs
    jdkPreference = "preferJre"
}

dependencies {

    implementation 'au.com.bytecode:opencsv:2.4'
    implementation 'com.github.lukeu:swing-dpi:0.6'

    // TODO: Would surely help code quality. Find out how to compact with proguard or something.
    // compile 'com.google.guava', version: '21.0'

    testImplementation 'junit:junit:4.12+'
}

